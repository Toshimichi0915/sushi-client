buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = 'https://plugins.gradle.org/m2/' }
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'eclipse'

archivesBaseName = 'sushi'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    runs {
        client {
            workingDirectory project.file('run')
            property 'fml.coreMods.load', 'net.toshimichi.sushi.mixin.MixinLoaderForge'
            property 'mixin.env.disableRefMap', 'true'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url = 'https://impactdevelopment.github.io/maven/' }
    maven { url = "https://jitpack.io" }
}

dependencies {
    implementation('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'launchwrapper'
        exclude module: 'log4j-core'
    }

    implementation('org.reflections:reflections:0.9.12') {
        exclude module: 'gson'
        exclude module: 'guava'
    }

    annotationProcessor('org.spongepowered:mixin:0.8.2:processor') {
        exclude module: 'gson'
    }

    implementation 'cabaletta:baritone-api:1.2'
    implementation 'com.github.cabaletta:baritone:1.2.14'
    implementation('org.slick2d:slick2d-core:1.0.2') {
        exclude module: 'lwjgl'
    }
    implementation 'slick-util:slick-util:1.0.0'
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
}

mixin {
    defaultObfuscationEnv 'searge'
    add sourceSets.main, 'mixins.sushi.refmap.json'
}

jar.finalizedBy('reobfJar')
jar {
    manifest.attributes(
            'Manifest-Version': 1.0,
            'MixinConfigs': 'mixins.sushi.json',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'FMLCorePluginContainsFMLMod': 'true',
            'FMLCorePlugin': 'net.toshimichi.sushi.mixin.MixinLoaderForge',
            'ForceLoadAsMod': 'true'
    )
}

tasks.build.dependsOn tasks.shadowJar
shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin'))
        include(dependency('org.reflections:reflections'))
        include(dependency('cabaletta:baritone-api'))
        include(dependency('org.slick2d:slick2d-core'))
        include(dependency('slick-util:slick-util'))
    }
    exclude 'LICENSE.txt'
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}
